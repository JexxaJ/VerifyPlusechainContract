// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract Voting is Ownable {
    IERC20 public token; // The ERC20 token used for voting
    mapping(uint256 => Candidate) public candidates;
    uint256 public totalCandidates;
    address[] public voterAddresses;
    mapping(uint256 => address[]) public candidateVoters;

    struct Voter {
        uint256 totalVotes;
        mapping(uint256 => uint256) votes;
    }

    struct Candidate {
        uint256 id;
        uint256 voteBalance;
    }

    mapping(address => Voter) public voters;

    event CandidateAdded(uint256 candidateId);
    event VoteCast(address voter, uint256 candidateId, uint256 amount);
    event VoteChanged(address voter, uint256 candidateId, uint256 amount);
    event VoteWithdrawn(address voter, uint256 candidateId, uint256 amount);

    constructor(address _token) Ownable(msg.sender) {
        token = IERC20(_token);
    }

    function addCandidate() public onlyOwner {
        totalCandidates++;
        candidates[totalCandidates] = Candidate(totalCandidates, 0);
        emit CandidateAdded(totalCandidates);
    }

    function addCandidatesInBulk(uint256 amount) external onlyOwner {
        for (uint256 i = 0; i < amount; i++) {
            addCandidate();
        }
    }

    function vote(uint256 candidateId, uint256 amount) external {
        require(candidates[candidateId].id != 0, "Candidate does not exist");
        require(
            token.balanceOf(msg.sender) >=
                voters[msg.sender].totalVotes + amount,
            "Not enough tokens to vote"
        );

        if (voters[msg.sender].votes[candidateId] > 0) {
            emit VoteChanged(msg.sender, candidateId, amount);
        } else {
            emit VoteCast(msg.sender, candidateId, amount);
            voterAddresses.push(msg.sender);
            candidateVoters[candidateId].push(msg.sender);
        }

        voters[msg.sender].totalVotes += amount;
        voters[msg.sender].votes[candidateId] += amount;
        candidates[candidateId].voteBalance += amount;

        require(
            token.transferFrom(msg.sender, address(this), amount),
            "Token transfer failed"
        );
    }

    function withdrawVote(uint256 candidateId) external {
        require(candidates[candidateId].id != 0, "Candidate does not exist");
        require(
            voters[msg.sender].votes[candidateId] > 0,
            "No votes to withdraw"
        );

        uint256 voteAmount = voters[msg.sender].votes[candidateId];
        voters[msg.sender].totalVotes -= voteAmount;
        candidates[candidateId].voteBalance -= voteAmount;
        emit VoteWithdrawn(msg.sender, candidateId, voteAmount);
        voters[msg.sender].votes[candidateId] = 0;

        for (uint256 i = 0; i < candidateVoters[candidateId].length; i++) {
            if (candidateVoters[candidateId][i] == msg.sender) {
                candidateVoters[candidateId][i] = candidateVoters[candidateId][
                    candidateVoters[candidateId].length - 1
                ];
                candidateVoters[candidateId].pop();
                break;
            }
        }

        require(
            token.transfer(msg.sender, voteAmount),
            "Token transfer failed"
        );
    }

    function endVoting() external onlyOwner {
        for (uint256 i = 0; i < voterAddresses.length; i++) {
            address voterAddress = voterAddresses[i];
            for (uint256 j = 1; j <= totalCandidates; j++) {
                if (voters[voterAddress].votes[j] > 0) {
                    uint256 voteAmount = voters[voterAddress].votes[j];
                    voters[voterAddress].totalVotes -= voteAmount;
                    candidates[j].voteBalance -= voteAmount;
                    voters[voterAddress].votes[j] = 0;

                    require(
                        token.transfer(voterAddress, voteAmount),
                        "Token transfer failed"
                    );
                }
            }
        }
    }

    function getCandidates() external view returns (Candidate[] memory) {
        Candidate[] memory candidateList = new Candidate[](totalCandidates);
        for (uint256 i = 1; i <= totalCandidates; i++) {
            candidateList[i - 1] = candidates[i];
        }
        return candidateList;
    }

    function getAllVoters()
        public
        view
        returns (address[] memory, uint256[] memory)
    {
        uint256 length = voterAddresses.length;

        uint256[] memory totalVotesArray = new uint256[](length);

        for (uint256 i = 0; i < length; i++) {
            address voterAddress = voterAddresses[i];
            totalVotesArray[i] = voters[voterAddress].totalVotes;
        }

        return (voterAddresses, totalVotesArray);
    }

    function getVotesForAllCandidates(
        address voterAddress
    ) public view returns (uint256[] memory, uint256[] memory) {
        uint256[] memory candidateIdsArray = new uint256[](totalCandidates);
        uint256[] memory votesArray = new uint256[](totalCandidates);

        for (uint256 i = 1; i <= totalCandidates; i++) {
            candidateIdsArray[i - 1] = i;
            votesArray[i - 1] = voters[voterAddress].votes[i];
        }

        return (candidateIdsArray, votesArray);
    }

    function getVotersForCandidate(
        uint256 candidateId
    ) external view returns (address[] memory) {
        return candidateVoters[candidateId];
    }
}
